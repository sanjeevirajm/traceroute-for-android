apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    namespace 'com.wandroid.traceroute'
    compileSdkVersion 36

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
        versionCode 2
        versionName "1.0.4"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_PLATFORM=latest"
                arguments "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=true"
            }
        }
    }

    buildTypes {
        release {
//            minifyEnabled true
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
//    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

afterEvaluate {
    android.libraryVariants.each { variant ->
        // Create variant-specific sources jar
        def sourcesJarTask = tasks.register("${variant.name}SourcesJar", Jar) {
            from android.sourceSets.main.java.srcDirs
            archiveClassifier = 'sources'
            archiveAppendix = variant.name
        }
        
        publishing.publications.create(variant.name, MavenPublication) {
            from components.findByName(variant.name)
            groupId = 'com.sanjeevirajm.traceroute-for-android'
            artifactId = "traceroute-for-android"
            version = '1.0.4'
            
            // Add variant-specific sources jar
            artifact sourcesJarTask
        }
    }
}